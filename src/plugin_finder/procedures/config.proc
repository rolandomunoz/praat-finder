# Copyright 2017-2021 Rolando Munoz

#Description: This script stablish a protocol to retrive and modify field information from "preferences folder files". 

#! ~~~ params
#! in:
#!	 .path$: the file directory where local variables are stored
#! out:
#!	 .return$: a dictionary which contains the variables names and their values
#! selection:
#!	 in:
#!		 nothing
#!	 out:
#!		 nothing
#! ~~~
#!
#! Read a file with local variables and store them in a dictionary
#!
procedure config.init: .path$
	.lines$# = readLinesFromFile$#(.path$)
	for .i to size(.lines$#)
		.key$ = replace_regex$(.lines$#[.i], "(.+): (.*)", "\1",1)
		.value$= replace_regex$(.lines$#[.i], "(.+): (.*)", "\2", 1)		
		.return$[.key$] = .value$ 		
	endfor
endproc

#! ~~~ params
#! in:
#!	 .key$: the name of the key
#! out:
#!	 .return$: the value
#! selection:
#!	 in:
#!		 nothing
#!	 out:
#!		 nothing
#! ~~~
#!
#! Get the value of the key
#!
procedure config.get_value: .key$
	if variableExists("config.init.return$[""'.key$'""]")
		.return$ = config.init.return$[.key$]
	else
		exitScript: "@config.get_value: '.key$' does not exist."
	endif
endproc

#! ~~~ params
#! in:
#!	 .key$: the name of the key
#!	 .value$: the value content of a key
#! selection:
#!	 in:
#!		 nothing
#!	 out:
#!		 nothing
#! ~~~
#!
#! Get the value of the key
#!
procedure config.set_value: .key$, .value$
	.missing_key = 1
	for .i to size(config.init.lines$#)
		if startsWith(config.init.lines$#[.i]+":", .key$)
			config.init.lines$#[.i] = .key$ + ": " + .value$
			.missing_key = 0
		endif
	endfor
	
	if .missing_key == 1
		exitScript: "@config.set_value: '.key$' does not exist"
	endif
endproc

procedure config.write
	first = 1
	for .i to size(config.init.lines$#)
		if first
			writeFileLine: config.init.path$, config.init.lines$#[.i]
			first = 0
		else
			appendFileLine: config.init.path$, config.init.lines$#[.i]			
		endif
	endfor
endproc 